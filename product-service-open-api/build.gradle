apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "org.openapi.generator"
apply plugin: 'com.moowork.node'

sourceSets {
    main {
        java {
            srcDirs = ["$buildDir/openapi-spring-mvc".toString()]
        }
    }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.1.0.RELEASE'
    compile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.21'
}

task openApiGenerateFinalSpec(type: Copy) {
    group = "openapi"
    from('src/main/api') {
        eachFile { filter { it.replaceAll('@VERSION@', project.version.toString()) } }
    }
    into "$buildDir/processed/$project.version"
}

task zipOpenapi(type: Zip) {
    group = "openapi"
    archiveName "${project.name}.zip"
    destinationDir file("$buildDir/dist/")
    from "$buildDir/processed/$project.version/"
}

publishing {
    publications {
        mavenDistribution(MavenPublication) {
            artifact source: zipOpenapi, extension: 'zip'
            artifact source: jar, extension: 'jar'
        }
    }
}

openApiValidate {
    inputSpec = "$buildDir/processed/${project.version}/openapi.yaml".toString()
}

task openApiGenerateAngular2Api(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask, dependsOn: tasks.openApiGenerateFinalSpec) {
    group = "openapi"
    generatorName = "typescript-angular"
    inputSpec = "$buildDir/processed/${project.version}/openapi.yaml".toString()
    outputDir = "$buildDir/openapi-typescript-angular/".toString()
}

task openApiGenerateApi(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask, dependsOn: tasks.openApiGenerateFinalSpec) {
    group = "openapi"
    generatorName = "spring"
    inputSpec = "$buildDir/processed/${project.version}/openapi.yaml".toString()
    outputDir = "$buildDir/openapi-spring-mvc/".toString()
    library = "spring-mvc"
    apiPackage = "com.rogerpeyer.productservice.api"
    modelPackage = "com.rogerpeyer.productservice.api.model"
    configOptions = [
            dateLibrary  : "java8",
            java8        : "true",
            interfaceOnly: "true"
    ]
}

node {
    // Set the work directory where node_modules should be located
    nodeModulesDir = file("$buildDir/openapi-typescript-angular/".toString())
}

compileJava.dependsOn tasks.openApiGenerateApi
tasks.openApiValidate.dependsOn tasks.openApiGenerateFinalSpec
check.dependsOn tasks.openApiValidate
npmInstall.dependsOn tasks.openApiGenerateAngular2Api