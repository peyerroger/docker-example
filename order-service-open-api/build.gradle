apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "org.openapi.generator"

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    "$buildDir/openapi/order-service-open-api".toString()
            ]
        }
    }
}

dependencies{
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.1.0.RELEASE'
    compile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.21'
}

task openApiGenerateFinalSpec(type: Copy) {
    group = "openapi"
    from ('src/main/api'){
        eachFile {filter{ it.replaceAll('@VERSION@', project.version.toString())}}
    }
    into "$buildDir/processed/$project.version"
}

task openapi(type: Zip) {
    group = "openapi"
    archiveName "${project.name}.zip"
    destinationDir file("$buildDir/dist/")
    from "$buildDir/processed/$project.version/"
}

publishing{
    publications {
        mavenDistribution(MavenPublication) {
            artifact source: openapi, extension: 'zip'
            artifact source: jar, extension: 'jar'
        }
    }
}

openApiValidate {
    inputSpec = "$buildDir/processed/$project.version/openapi.yaml".toString()
}

task openApiGenerateApi(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    group = "openapi"
    generatorName = "spring"
    inputSpec = "$buildDir/processed/$project.version/openapi.yaml".toString()
    outputDir = "$buildDir/openapi/order-service-open-api/".toString()
    library = "spring-mvc"
    apiPackage = "com.rogerpeyer.orderservice.api"
    modelPackage = "com.rogerpeyer.orderservice.api.model"
    configOptions = [
            dateLibrary  : "java8",
            java8        : "true",
            interfaceOnly: "true"
    ]
}

tasks.openApiValidate.dependsOn tasks.openApiGenerateFinalSpec
check.dependsOn tasks.openApiValidate
compileJava.dependsOn tasks.openApiGenerateApi
tasks.openApiGenerateApi.dependsOn tasks.openApiGenerateFinalSpec
