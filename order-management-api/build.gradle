import org.openapitools.generator.gradle.plugin.tasks.*
import org.springframework.boot.gradle.plugin.SpringBootPlugin

apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "org.openapi.generator"
apply plugin: 'com.moowork.node'
apply plugin: "io.spring.dependency-management"

/*
 ***************************************************************************************************
 ********* CONFIGURATIONS **************************************************************************
 ***************************************************************************************************
 */
configurations {
    springMvcServer
    springMvcServerCompile.extendsFrom compile
    springMvcServerRuntime.extendsFrom runtime
    springRestTemplate
    springRestTemplateCompile.extendsFrom compile
    springRestTemplateRuntime.extendsFrom runtime
    openApi
}

/*
 ***************************************************************************************************
 ********* SOURCE SETS **************************************************************************
 ***************************************************************************************************
 */
sourceSets {
    springMvcServer {
        java {
            srcDirs = [
                    "$buildDir/openapi-spring-mvc/src/main/java".toString()
            ]
        }
    }
    springRestTemplate {
        java {
            srcDirs = [
                    "$buildDir/openapi-spring-resttemplate/src/main/java".toString()
            ]
        }
    }
}

/*
 ***************************************************************************************************
 ********* DEPENDENCIES ****************************************************************************
 ***************************************************************************************************
 */
dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.21'
}

/*
 ***************************************************************************************************
 ********* OPENAPI FINAL SPEC **********************************************************************
 ***************************************************************************************************
 */
task openApiGenerateFinalSpec(type: Copy) {
    group = "openapi"
    from('src/main/api') {
        eachFile { filter { it.replaceAll('@VERSION@', project.version.toString()) } }
    }
    into "$buildDir/processed/$project.version"
}

/*
 ***************************************************************************************************
 ********* OPENAPI CODE GENERATORS *****************************************************************
 ***************************************************************************************************
 */
task angular7ClientGenerate(type: GenerateTask) {
    group = "openapi"
    generatorName = "typescript-angular"
    inputSpec = "$projectDir/src/main/api/openapi.yaml".toString()
    outputDir = "$buildDir/openapi-typescript-angular/".toString()
    configOptions = [
            ngVersion    : 7,
            npmName      : "@rogerpeyer/${project.name}-angular-7",
            npmVersion   : "${project.version}",
            npmRepository: "http://localhost:8081/repository/npm-hosted/",
            snapshot     : false
    ]
}

task springMvcServerGenerate(type: GenerateTask) {
    group = "openapi"
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/api/openapi.yaml".toString()
    outputDir = "$buildDir/openapi-spring-mvc/".toString()
    library = "spring-mvc"
    apiPackage = "com.rogerpeyer.ordermanagement.api"
    modelPackage = "com.rogerpeyer.ordermanagement.api.model"
    configOptions = [
            dateLibrary  : "java8",
            java8        : "true",
            interfaceOnly: "true"
    ]
}

task springRestTemplateGenerate(type: GenerateTask) {
    group = "openapi"
    generatorName = "java"
    inputSpec = "$projectDir/src/main/api/openapi.yaml".toString()
    outputDir = "$buildDir/openapi-spring-resttemplate/".toString()
    library = "resttemplate"
    apiPackage = "com.rogerpeyer.ordermanagement.client.api"
    modelPackage = "com.rogerpeyer.ordermanagement.client.api.model"
    configOptions = [
            dateLibrary: "java8",
            java8      : "true"
    ]
}

/*
 ***************************************************************************************************
 ********* MAVEN ARTIFACTS *************************************************************************
 ***************************************************************************************************
 */

// Build artifacts
task springMvcServerJar(type: Jar) {
    group = "build"
    archiveName = "${project.name}.jar"
    from(sourceSets.springMvcServer.output) {
        include '**/*.class'
    }
}

task springRestTemplateJar(type: Jar) {
    group = "build"
    archiveName = "${project.name}.jar"
    from(sourceSets.springRestTemplate.output) {
        include '**/*.class'
    }
}

task openApiZip(type: Zip, dependsOn: openApiGenerateFinalSpec) {
    group = "build"
    archiveName "${project.name}.zip"
    destinationDir file("$buildDir/dist/")
    from "$buildDir/processed/$project.version/"
}

// Define artifacts
artifacts {
    openApi openApiZip
    springMvcServer springMvcServerJar
    springRestTemplate springRestTemplateJar
}

/*
 ***************************************************************************************************
 ********* PUBLISHING ******************************************************************************
 ***************************************************************************************************
 */

// maven
publishing {
    publications {
        openApiPublication(MavenPublication) {
            artifactId "${project.name}-openapi"
            artifact openApiZip
        }
        springRestTemplatePublication(MavenPublication) {
            artifactId "${project.name}-spring-resttemplate"
            artifact springRestTemplateJar
        }
        springMvcServerPublication(MavenPublication) {
            artifactId "${project.name}-spring-mvc"
            artifact springMvcServerJar
        }
    }
}

// npm
task publishAngular7ClientToNpm(type: NpmTask, dependsOn: tasks.angular7ClientGenerate) {
    group = "publishing"
    environment = ['AUTH_TOKEN': project.property('npm.token')]
    workingDir = file("$buildDir/openapi-typescript-angular/".toString())
    args = ['publish']
}

/*
 ***************************************************************************************************
 ********* API Validation **************************************************************************
 ***************************************************************************************************
 */
openApiValidate {
    inputSpec = "$projectDir/src/main/api/openapi.yaml".toString()
}

/*
 ***************************************************************************************************
 ********* NODE SETUP ******************************************************************************
 ***************************************************************************************************
 */
node {
    distBaseUrl = "http://localhost:8081/repository/npm-group/"
}

/*
 ***************************************************************************************************
 ********* TASK DEPENDENCIES ***********************************************************************
 ***************************************************************************************************
 */
compileJava.dependsOn compileSpringMvcServerJava, compileSpringRestTemplateJava

compileSpringMvcServerJava.dependsOn tasks.springMvcServerGenerate
compileSpringRestTemplateJava.dependsOn tasks.springRestTemplateGenerate

check.dependsOn tasks.openApiValidate
publish.dependsOn tasks.publishAngular7ClientToNpm
