apply plugin: "base"
apply plugin: "maven-publish"
apply plugin: "org.openapi.generator"

task openApiGenerateFinalSpec(type: Copy) {
    group = "openapi"
    from ('src/main/api'){
        eachFile {filter{ it.replaceAll('@VERSION@', project.version.toString())}}
    }
    into "$buildDir/openapi/spec/$project.version"
}

task copyFinalSpecToDist(type: Copy) {
    group = "documentation"
    into file("$buildDir/doc/dist/openapi")
    from "$buildDir/openapi/spec/$project.version"
}

task copyProtobufSpecToDist(type: Copy) {
    group = "documentation"
    from 'src/main/protoc'
    into file("$buildDir/doc/dist/protobuf")
}

task documentationAssembleArtifact(type: Zip) {
    group = "documentation"
    archiveName "${project.name}.zip"
    destinationDir file("$buildDir/doc/zip/")
    from "$buildDir/doc/dist/"
}

publishing{
    publications {
        mavenDistribution(MavenPublication) {
            artifact source: documentationAssembleArtifact, extension: 'zip'
        }
    }
}

openApiValidate {
    inputSpec = "$buildDir/openapi/spec/$project.version/openapi.yaml".toString()
}

tasks.documentationAssembleArtifact.dependsOn tasks.copyFinalSpecToDist
tasks.documentationAssembleArtifact.dependsOn tasks.copyProtobufSpecToDist
tasks.copyFinalSpecToDist.dependsOn tasks.openApiGenerateFinalSpec
tasks.openApiValidate.dependsOn tasks.openApiGenerateFinalSpec
check.dependsOn tasks.openApiValidate
